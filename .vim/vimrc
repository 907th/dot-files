set nocompatible

" vim-plug {{{

call plug#begin('~/.vim/plugged')

Plug 'jeffkreeftmeijer/vim-dim'
Plug 'vim-airline/vim-airline'
Plug 'jeetsukumaran/vim-buffergator'
Plug 'schickling/vim-bufonly'
Plug 'szw/vim-maximizer'
Plug 'majutsushi/tagbar'
Plug 'junegunn/fzf'
Plug 'jiangmiao/auto-pairs'
Plug 'mileszs/ack.vim'
Plug 'bronson/vim-trailing-whitespace'
Plug 'Lokaltog/vim-easymotion'
Plug 'junegunn/vim-easy-align'
Plug 'dockyard/vim-easydir'
Plug 'MarcWeber/vim-addon-mw-utils' " dependency of vim-snipmate
Plug 'tomtom/tlib_vim' " dependency of vim-snipmate
Plug 'garbas/vim-snipmate'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-vinegar'
Plug 'tpope/vim-abolish'
Plug 'tpope/vim-rails'
Plug 'tommcdo/vim-fubitive'
Plug 'vim-ruby/vim-ruby'
Plug 'noprompt/vim-yardoc'
Plug 'honza/dockerfile.vim'
Plug 'pangloss/vim-javascript'
Plug 'posva/vim-vue'
Plug 'cakebaker/scss-syntax.vim'
Plug '907th/vim-auto-save'

call plug#end()

" }}}

" Main settings {{{

syntax on
colorscheme dim

" Enable RGB colors
set termguicolors
let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"

filetype on
filetype plugin on
filetype indent on

set autoindent
set autoread
set number
set relativenumber
set wrap
set hidden
set fileencoding=utf-8
set mouse=a
set mousehide
set ttymouse=xterm2
set backspace=2
set laststatus=2
set scrolloff=2
set sidescrolloff=4
set incsearch
set hlsearch
set ignorecase
set smartcase
set foldenable
set foldmethod=indent
set foldlevelstart=99
set showcmd
set wildmenu
set wildmode=list:longest
set completeopt=menuone,preview
set nobackup
set nowritebackup
set noswapfile
set listchars=tab:\ \ ,trail:.
set list
set splitright
set splitbelow
set noshowmode
set diffopt=filler,vertical
set expandtab
set tabstop=2
set shiftwidth=2
set softtabstop=0

set cpoptions+=I " Do not delete autoindentation after cursor movement

" This always sets format options (https://vi.stackexchange.com/a/9367)
autocmd FileType * setlocal
\ formatoptions-=c
\ formatoptions-=r
\ formatoptions-=o

" }}}

" Plugins config {{{

let g:buffergator_sort_regime = 'mru'
let g:buffergator_show_full_directory_path = 0
let g:buffergator_autoexpand_on_split = 0
let g:buffergator_viewport_split_policy  = 'B'
let g:buffergator_split_size = 12
let g:buffergator_suppress_keymaps = 1

let g:tagbar_compact = 1
let g:tagbar_autofocus = 1
let g:tagbar_autoshowtag = 1
let g:tagbar_width = 35
let g:tagbar_left = 1
let g:tagbar_sort = 0

let g:ackprg = 'ag --vimgrep'
cnoreabbrev Ag Ack!

let g:typescript_compiler_binary = './npm_exec tsc'

let g:maximizer_set_default_mapping = 0
let g:maximizer_restore_on_winleave = 1

let g:AutoPairsCenterLine = 0
let g:AutoPairsMultilineClose = 0
let g:AutoPairsShortcutFastWrap = '<Leader>pp'
let g:AutoPairsShortcutBackInsert = '<Leader>pi'

let g:javascript_plugin_jsdoc = 1

let g:snipMate = { 'snippet_version' : 1 }

" }}}

" Key mappings {{{
" Hint: Use `sed -n l` to debug which sequence terminal sends on key press

nmap <Down> gj
nmap <Up> gk
vmap <Down> gj
vmap <Up> gk

noremap <expr> <Home> col('.') == match(getline('.'),'\S')+1 ? '0' : '^'
imap <Home> <C-o><Home>

nnoremap <CR> o<Esc>
nnoremap <Backspace> O<Esc>
nnoremap <C-J> i<CR><Esc>

nnoremap <Del> a<Space><Esc>
nnoremap <Space> i<Space><Esc>

xmap ga <Plug>(EasyAlign)
nmap ga <Plug>(EasyAlign)

" <S-Left/Right>
nmap <silent> [1;2D :tabprev<CR>
nmap <silent> [1;2C :tabnext<CR>

" <M-Left/Right/Up/Down>
nmap <silent> [1;3D <C-w>h
nmap <silent> [1;3C <C-w>l
nmap <silent> [1;3A <C-w>k
nmap <silent> [1;3B <C-w>j

nmap <Leader>1 :BuffergatorToggle<CR>
nmap <Leader>2 :TagbarToggle<CR>
nmap <Leader>9 :Reset<CR>
nmap <Leader>a :w<CR>
nmap <Leader>q :q<CR>
nmap <Leader>x :bd<CR>
nmap <Leader>s <C-w>s
nmap <Leader>v <C-w>v
nmap <Leader>c <C-w>c
nmap <Leader>w :tabnew<CR>
nmap <Leader>z :MaximizerToggle!<CR>
nmap <Leader>f :FZF<CR>
nmap <Leader>] :FixWhitespace<CR>
nmap <Leader>/ :let @/ = ''<CR>
nmap <Leader>ee :%s/
nmap <Leader>ew :%s/<C-R><C-W>/

nmap <silent> <Leader>yy :let @+ = fnamemodify(expand("%"), ":~:.")<CR>
nmap <silent> <Leader>yl :let @+ = fnamemodify(expand("%"), ":~:.") . ":" . line(".")<CR>

command Reset tabnew | tabonly | BufOnly

" }}}

" Tab autocompletion + snippets {{{

imap <expr> <CR> <SID>myCR()
imap <expr> <Tab> <SID>myTab()
imap <expr> <Up> <SID>myUp()
imap <expr> <Down> <SID>myDown()

function! s:myCR()
  return pumvisible() ? "\<C-y>" : "\<CR>\<Space>\<BS>"
endfunction

function! s:triggerSnippet()
  return "\<C-r>=snipMate#TriggerSnippet()\<CR>"
endfunction

function! s:myTab()
  " Clear SnipMate tab stops
  if exists('b:snip_state')
    call b:snip_state.remove()
  endif
  return pumvisible() ? "\<C-e>" :
\     <SID>snipMateCanBeExpanded() ? <SID>triggerSnippet() :
\       <SID>checkBackSpace() ?  "\<Tab>" :
\       <SID>autoCompleteFunction()
endfunction

function! s:autoCompleteFunction()
  if &ft == 'css' || &ft == 'sass' || &ft == 'scss'
    return "\<C-x>\<C-o>"
  else
    return "\<C-n>"
  endif
endfunction

function! s:myUp()
  return pumvisible() ? "\<C-p>" : "\<Up>"
endfunction

function! s:myDown()
  return pumvisible() ? "\<C-n>" : "\<Down>"
endfunction

function! s:checkBackSpace()
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~ '\s'
endfunction

function! s:snipMateCanBeExpanded()
  let word     = snipMate#WordBelowCursor()
  let snippets = map(snipMate#GetSnippetsForWordBelowCursor(word, 0), 'v:val[0]')
  let matches  = filter(snippets, "v:val =~# '\\V\\^" . escape(word, '\') . "\\$'")
  return len(matches) > 0
endfunction

" }}}

